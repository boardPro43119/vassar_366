### english.foma ###
# Benjamin Prud'homme, March 4, 2021, bprudhomme@vassar.edu

# Vowels
define V [ a | e | i | o | u ];

read lexc english.lexc
define Lexicon;

# Consonant doubling: 1-letter consonant doubled before -ing/-ed/-able
define ConsonantDoublingG g -> g g || _ "^" [i n g | e d | a b l e];
define ConsonantDoublingP p -> p p || _ "^" [i n g | e d | a b l e];
define ConsonantDoublingD d -> d d || V _ "^" [i n g | e d | a b l e];

# E deletion: silent e dropped before -ing/-ed/er/-est/-able
define EDeletion e -> 0 || _ "^" [ i n g | e d | e r | e s t | a b l e] ;

# E insertion e added after -s, -z, -x, -ch, -sh before s (watch/watches)
define EInsertion [..] -> e || s | z | x | c h | s h _ "^" s ;

# Y replacement: -y changes to -ie before -s, -i before -ed (try/tries) or -able (triable)
define YReplacement y -> i e || _ "^" s    ,,
                    y -> i   || _ "^" [e d | a b l e]  ;

# K insertion: verbs ending with vowel + -c add -k (panic/panicked)
define KInsertion [..] -> k || V c _ "^" [e d | i n g];

# -LY merge: -able changes to -ably
define LYMerge l e -> 0 || _ "^" l y;

# Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;

define Grammar Lexicon            .o.
               ConsonantDoublingG .o.
	       ConsonantDoublingP .o.
               ConsonantDoublingD .o.
               EDeletion          .o.
               EInsertion         .o.
               YReplacement       .o.
               KInsertion         .o.
	       LYMerge		  .o.
               Cleanup;

# Exceptions (past forms of make/write/drink, plurals of mouse/child, makeable doesn't drop silent e when adding -able)
define Exceptions [[{make} "+V" "+Past" .x. {made}] | [{make} "+V" "+PastPart" .x. {made}] | [{write} "+V" "+Past" .x. {wrote}] | [{write} "+V" "+PastPart" .x. {written}] | [{drink} "+V" "+Past" .x. {drank}] | [{drink} "+V" "+PastPart" .x. {drunk}] | [{mouse} "+N" "+Pl" .x. {mice}] | [{child} "+N" "+Pl" .x. {children}] | [{make} "^able" "+Adj" .x. {makeable}]] ;

# Parallel forms (cactus, octopus, sneak)
define ParallelForms [[c a c t u s %+N %+Pl .x. c a c t i] | [o c t o p u s %+N %+Pl .x. o c t o p i] | [s n e a k %+V %+Past .x. s n u c k] | [s n e a k %+V %+PastPart .x. s n u c k]];

regex Exceptions .P. [Grammar | ParallelForms] ;
